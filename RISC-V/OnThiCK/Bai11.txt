# Đề 11: Kiểm tra N chia hết cho 0x14 và lớn hơn 0x41
# N = SW[9:2] (8-bit)

.equ LEDS, 0xff200000
.equ SWITCHES, 0xff200040

.text
.global _start
_start:
	li sp, 0x03FFFFFC
	
main_loop:
	la t0, SWITCHES
	lw t1, 0(t0)
	srli t1, t1, 2
	andi a0, t1, 0xFF
	
	jal ra, kiem_tra
	
	la t0, LEDS
	sw a0, 0(t0)
	
	j main_loop
	
kiem_tra:
	addi sp, sp, -16
	sw s0, 0(sp) # N
	sw s1, 4(sp) # Điều kiện 1
	sw s2, 8(sp) # Điều kiện 2
	sw ra, 12(sp)
	
	mv s0, a0 # s0 = N
	li s1, 0 # Điều kiện 1 chia hết 0x14
	li s2, 0 # Điều kiện 2 lớn hơn 0x41
	
	# Kiểm tra điều kiện 1
	mv a0, s0
	jal ra, chia_het
	mv s1, a0
	
	# Kiểm tra điều kiện 2
	li t0, 0x41
	bgt s0, t0, dieu_kien_2_dung
	j check_result # s2 = 0 do li s2 = 0 ko có gì thay đổi
	
dieu_kien_2_dung:
	li s2, 1 # s2 = 1

check_result:
	add t0, s1, s2
	bne t0, zero, check_both
	li a0, 0
	j kiem_tra_end
	
check_both:
	li t1, 2
	bne t0, t1, thoa_man_1_dieu_kien
	li a0, 0x3FF
	j kiem_tra_end
	
thoa_man_1_dieu_kien:
	li a0, 0xF
	
kiem_tra_end:
	lw s0, 0(sp)
	lw s1, 4(sp)
	lw s2, 8(sp)
	lw ra, 12(sp)
	addi sp, sp, 16
	
	ret
	

chia_het:
	addi sp, sp, -8
	sw s0, 0(sp)
	sw ra, 4(sp)
	
	mv s0, a0
	
	li t0, 0x14
	
chia_het_loop:
	blt s0, t0, chia_het_check
	sub s0, s0, t0
	j chia_het_loop
	
chia_het_check:
	beq s0, zero, dieu_kien_1_dung
	li a0, 0
	j chia_het_end
	
dieu_kien_1_dung:
	li a0, 1
	
chia_het_end:
	lw s0, 0(sp)
	lw ra, 4(sp)
	addi sp, sp, 8
	
	ret
	

# TEST CASES:
# SW[9:2] = 01010000 (N=80=0x50): LED = 0x3FF  80%20=0 ✓, 80>65 ✓ → cả 2 điều kiện
# SW[9:2] = 00010100 (N=20=0x14): LED = 0xF    20%20=0 ✓, 20>65 ✗ → 1 điều kiện
# SW[9:2] = 01000010 (N=66=0x42): LED = 0xF    66%20=6 ✗, 66>65 ✓ → 1 điều kiện
# SW[9:2] = 00001111 (N=15=0x0F): LED = 0x0    15%20=15 ✗, 15>65 ✗ → 0 điều kiện
# SW[9:2] = 00000000 (N=0): LED = 0xF          0%20=0 ✓, 0>65 ✗ → 1 điều kiện
# SW[9:2] = 01100100 (N=100=0x64): LED = 0x3FF 100%20=0 ✓, 100>65 ✓ → cả 2 điều kiện