# Đề 3: Tính UCLN của N và M
# N từ SW[3:0], M từ SW[7:4]
# x11 = UCLN(N, M) và x11 được gán vào a1

.equ LEDS, 0xff200000
.equ SWITCHES, 0xff200040

.text
.global _start

_start:
	li sp, 0x03FFFFFC
	
main_loop:
	la t0, SWITCHES
	lw t1, 0(t0)
	andi a0, t1, 0xF #N
	srli t1, t1, 4
	andi a1, t1, 0xF #M
	
	jal ra, tinh_ucln
	
	la t0, LEDS
	sw a1, 0(t0)
	
	j main_loop
	
tinh_ucln:
	addi sp, sp, -12
	sw ra, 0(sp)
	sw s0, 4(sp)
	sw s1, 8(sp)
	
	mv s0, a0
	mv s1, a1
	
	bne s0, zero, check_m # check N khác 0 hay không
	mv a1, s1 # N = 0 trả giá trị của M (ví dụ UCLN(5,0) = 5 )
	j ucln_end
	
check_m:
	bne s1, zero, ucln_loop
	mv a1, s0
	j ucln_end 
	
ucln_loop:
	beq s0, s1, ucln_found # N = M thì nhảy tới found
	blt s0, s1, ucln_swap # N < M thì nhảy tới swap
	sub s0, s0, s1 # N > M thì làm phép trừ N - M lưu vào N
	j ucln_loop
	
ucln_swap: #h=khi N < M
	sub s1, s1, s0 # lấy M - N lưu vào M
	j ucln_loop
	
ucln_found:
	mv a1, s0 # chọn s1 cũng được vì N = M (VD : UCLN(5,5) = 5 )
	
ucln_end:
	lw ra, 0(sp)
	lw s0, 4(sp)
	lw s1, 8(sp)
	addi sp, sp, 12
	
	ret
# TEST CASES:
# SW[7:4]=0011, SW[3:0]=0010 (M=3, N=2): LED = 0x001  UCLN(2,3)=1
# SW[7:4]=0100, SW[3:0]=0110 (M=4, N=6): LED = 0x002  UCLN(6,4)=2
# SW[7:4]=0110, SW[3:0]=1001 (M=6, N=9): LED = 0x003  UCLN(9,6)=3
# SW[7:4]=1000, SW[3:0]=1100 (M=8, N=12): LED = 0x004 UCLN(12,8)=4
# SW[7:4]=0101, SW[3:0]=0111 (M=5, N=7): LED = 0x001  UCLN(7,5)=1
# SW[7:4]=0000, SW[3:0]=0101 (M=0, N=5): LED = 0x005  UCLN(5,0)=5